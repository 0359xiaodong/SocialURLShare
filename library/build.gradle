apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.elsinga.socialurlshare"

        minSdkVersion 8
        targetSdkVersion 19

        versionName VERSION_NAME
        versionCode Integer.parseInt(VERSION_CODE)
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }

    signingConfigs {
        release {
            def Properties localProps = new Properties()
            localProps.load(new FileInputStream(file('../local.properties')))
            def Properties keyProps = new Properties()
            if(localProps.containsKey('keystore.props.file')) {
                assert localProps['keystore.props.file'];
                keyProps.load(new FileInputStream(file(localProps['keystore.props.file'])))
                storeFile file(keyProps["store"])
                keyAlias keyProps["alias"]
                storePassword keyProps["storePass"]
                keyPassword keyProps["pass"]
            } else {
                println 'Probably your local.properties that the library module uses isn\'t correctly setup.'
            }
        }
    }

    buildTypes {
        debug {
            debuggable true
            runProguard false

            signingConfig signingConfigs.debug
        }

        release {
            runProguard false
            signingConfig signingConfigs.release
        }
    }
}

// Used to push in maven
apply from: '../maven_push.gradle'
